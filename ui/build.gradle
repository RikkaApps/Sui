import java.nio.file.Paths

plugins {
    id('com.android.application')
    id('kotlin-android')
    id('dev.rikka.tools.hidden-api-refine')
}

hiddenApiRefine {
    refinePrefix = ['$']
    log = true
}

apply from: file(rootProject.file('module.gradle'))

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode gitCommitCount
        versionName moduleVersion.substring(1)
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        debug {
            multiDexEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude '/META-INF/*.version'
        exclude '/META-INF/*.kotlin_module'
        exclude '/kotlin/**'
        exclude '**.bin'
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dependenciesInfo {
        includeInApk false
    }
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

repositories {
    mavenLocal()
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1"

    implementation project(':aidl')
    implementation project(':api')
    compileOnly project(':hidden-api-stub')
    implementation 'dev.rikka.rikkax.parcelablelist:parcelablelist:1.0.0'

    implementation 'dev.rikka.rikkax.appcompat:appcompat:1.2.0-rc01'
    implementation 'dev.rikka.rikkax.material:material:1.6.6'
    implementation 'dev.rikka.rikkax.insets:insets:1.1.0'
    implementation 'dev.rikka.rikkax.lifecycle:lifecycle-resource-livedata:1.0.0'
    implementation 'dev.rikka.rikkax.lifecycle:lifecycle-viewmodel-lazy:1.0.0'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-adapter:1.3.0'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-ktx:1.2.2'
    implementation 'dev.rikka.rikkax.widget:borderview:1.0.1'
    implementation 'dev.rikka.rikkax.html:html-ktx:1.1.2'

    implementation "androidx.annotation:annotation:1.2.0"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'

    implementation 'me.zhanghai.android.fastscroll:library:1.1.6'
    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.3.1'
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        def variantCapped = variant.name.capitalize()
        def variantLowered = variant.name.toLowerCase()

        if (variant.getBuildType().isMinifyEnabled()) {
            def processResourcesTask = tasks.getByName("process${variantCapped}Resources")
            def optimizeResourceTask = task("optimize${variantCapped}Resources").doLast {
                def aapt2 = Paths.get(project.android.sdkDirectory.path, 'build-tools', project.android.buildToolsVersion, 'aapt2')
                def zip = Paths.get(project.buildDir.path, 'intermediates',
                        'processed_res', variantLowered, 'out', "resources-${variantLowered}.ap_")
                def map = Paths.get(project.buildDir.path, 'outputs',
                        'mapping', variantLowered, 'resources.txt')
                def optimized = new File("${zip}.opt")
                def cmd = exec {
                    commandLine aapt2, 'optimize', '--collapse-resource-names',
                            '--shorten-resource-paths',
                            '--resources-config-path', 'aapt2-resources.cfg',
                            '--resource-path-shortening-map', map,
                            '-o', optimized, zip
                    ignoreExitValue false
                }
                if (cmd.exitValue == 0) {
                    delete(zip)
                    optimized.renameTo("$zip")
                }
            }
            optimizeResourceTask.onlyIf {
                !processResourcesTask.state.upToDate
            }
            processResourcesTask.finalizedBy(optimizeResourceTask)
        }
    }
}
