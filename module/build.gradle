import org.apache.tools.ant.filters.FixCrLfFilter

plugins {
    id('com.android.application')
    id('kotlin-android')
    id('dev.rikka.tools.hidden-api-refine')
}

hiddenApiRefine {
    refinePrefix = ['$']
    log = true
}

apply from: file(rootProject.file('module.gradle'))

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode gitCommitCount
        versionName moduleVersion.substring(1)
        externalNativeBuild {
            cmake {
                arguments "-DMODULE_NAME:STRING=$moduleId",
                        "-DRIRU_MODULE_API_VERSION=$moduleRiruApiVersion",
                        "-DRIRU_MODULE_VERSION=$moduleVersionCode",
                        "-DRIRU_MODULE_VERSION_NAME:STRING=\"$moduleVersion\""

            }
        }
    }
    buildFeatures {
        viewBinding true
        prefab true
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    buildTypes {
        debug {
            multiDexEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dependenciesInfo {
        includeInApk false
    }
}

dependencies {
    implementation 'dev.rikka.ndk:riru:25.0.0'
    implementation 'dev.rikka.ndk.thirdparty:nativehelper:1.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "androidx.annotation:annotation:1.2.0"

    implementation project(':aidl')
    implementation project(':shared')
    implementation project(':api')
    compileOnly project(':hidden-api-common')
    compileOnly project(':hidden-api-21')
}

def outDir = file("$rootDir/out")


import java.nio.file.Files
import java.security.MessageDigest

static def calcSha256(file) {
    def md = MessageDigest.getInstance("SHA-256")
    file.eachByte 4096, { bytes, size ->
        md.update(bytes, 0, size);
    }
    return md.digest().encodeHex()
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        def variantCapped = variant.name.capitalize()
        def variantLowered = variant.name.toLowerCase()

        def zipName = "${moduleId}-${moduleVersion}-${variantLowered}.zip"
        def magiskDir = file("$outDir/magisk_module_${variantLowered}")
        def isWindows = org.gradle.internal.os.OperatingSystem.current().isWindows()

        task("zip${variantCapped}", type: Zip) {
            dependsOn("prepareMagiskFiles${variantCapped}")
            from magiskDir
            archiveName zipName
            destinationDir outDir
        }

        task("prepareMagiskFiles${variantCapped}") {
            dependsOn("assemble$variantCapped")

            doFirst {
                delete { delete magiskDir }
                copy {
                    from "$rootDir/template/magisk_module"
                    into magiskDir.path
                }
                copy {
                    from "$rootDir/template/magisk_module"
                    into magiskDir.path
                    include 'riru.sh'
                    filter { line ->
                        line.replaceAll('%%%RIRU_MODULE_ID%%%', moduleId)
                                .replaceAll('%%%RIRU_MODULE_API_VERSION%%%', moduleRiruApiVersion.toString())
                                .replaceAll('%%%RIRU_MODULE_MIN_API_VERSION%%%', moduleMinRiruApiVersion.toString())
                                .replaceAll('%%%RIRU_MODULE_MIN_RIRU_VERSION_NAME%%%', moduleMinRiruVersionName)
                    }
                    filter(FixCrLfFilter.class,
                            eol: FixCrLfFilter.CrLf.newInstance("lf"))
                }
                Files.copy(file("$rootDir/template/magisk_module/.gitattributes").toPath(), file("${magiskDir.path}/.gitattributes").toPath())

                def modulePropText = ""
                moduleProp.each { k, v -> modulePropText += "$k=$v\n" }
                modulePropText = modulePropText.trim()
                file("$magiskDir/module.prop").text = modulePropText
            }

            def nativeOutDir = file("build/intermediates/cmake/${variant.name}/obj")
            def apk
            if (variant.name == "release")
                apk = file("build/outputs/apk/$variant.name/${project.name}-${variant.name}-unsigned.apk")
            else
                apk = file("build/outputs/apk/$variant.name/${project.name}-${variant.name}.apk")

            doLast {
                copy {
                    from "$nativeOutDir"
                    into "$magiskDir/lib"
                    exclude "**/*.txt"
                }
                copy {
                    from zipTree(apk).matching { include 'classes.dex' }.singleFile
                    into magiskDir
                    rename { fileName -> 'sui.dex' }
                }
                copy {
                    from zipTree(apk).matching { include 'res/*/*.xml' }
                    into magiskDir
                }

                // generate sha1sum
                fileTree("$magiskDir").matching {
                    exclude "README.md", "META-INF"
                }.visit { f ->
                    if (f.directory) return
                    file(f.file.path + ".sha256sum").text = calcSha256(f.file)
                }
            }
        }

        task("push${variantCapped}", type: Exec) {
            dependsOn("assemble${variantCapped}")
            workingDir outDir
            def commands = [android.adbExecutable, "push",
                            zipName,
                            "/data/local/tmp/"]
            if (isWindows) {
                commandLine 'cmd', '/c', commands.join(" ")
            } else {
                commandLine commands
            }
        }

        task("flash${variantCapped}", type: Exec) {
            dependsOn("push${variantCapped}")
            def commands = [android.adbExecutable, "shell", "su", "-c",
                            "magisk --install-module /data/local/tmp/${zipName}"]
            if (isWindows) {
                commandLine 'cmd', '/c', commands.join(" ")
            } else {
                commandLine commands
            }
        }

        task("flashAndReoot${variantCapped}", type: Exec) {
            dependsOn("flash${variantCapped}")
            def commands = [android.adbExecutable, "shell", "reboot"]
            if (isWindows) {
                commandLine 'cmd', '/c', commands.join(" ")
            } else {
                commandLine commands
            }
        }

        variant.assembleProvider.get().finalizedBy("zip${variantCapped}")
    }
}
